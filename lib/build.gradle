
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'it.scoppelletti.spaceship.android-library'
    id 'org.jetbrains.dokka'
}

group 'it.scoppelletti.spaceship'
version '1.1.0'
description 'Spaceship for HTTP'
archivesBaseName = 'spaceship-http'

android {
    namespace 'it.scoppelletti.spaceship.http'
    compileSdk sdk_target
    defaultConfig {
        minSdk sdk_min
        targetSdk sdk_target
        vectorDrawables.useSupportLibrary = true
        aarMetadata {
            minCompileSdk = sdk_min
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

kotlin {
    jvmToolchain(17)
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-http'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-http.git'
    inceptionYear = '2017-2021'
}

configurations {
    bom
    api.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(libs.bom.kotlin)
    bom platform(libs.bom.spaceship)
    api 'androidx.annotation:annotation'
    api 'androidx.appcompat:appcompat'
    api 'androidx.constraintlayout:constraintlayout'
    api 'com.google.android.material:material'
    api 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    api 'com.squareup.okhttp3:okhttp'
    api 'com.squareup.retrofit2:retrofit'
    api 'it.scoppelletti.spaceship:spaceship'
    api 'it.scoppelletti.spaceship:spaceship-stdlib'
    implementation 'com.squareup.okio:okio'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.slf4j:slf4j-api'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
